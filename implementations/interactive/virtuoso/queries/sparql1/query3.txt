sparql select ?fr ?first ?last ?ct1 ?ct2 ((?ct1 + ?ct2) as ?sum) #Q3
where { 
     {select distinct ?fr ?first ?last ?ct1 ?ct2
      where {
         {select ?fr (count(*) as ?ct1)
          where {
            ?post snvoc:hasCreator ?fr .
            ?post snvoc:creationDate ?date .
            filter (?date >= "%Date0%"^^xsd:dateTime && ?date < "%Date0%"^^xsd:dateTime + xsd:duration("P%Duration%D")) .
            ?post snvoc:isLocatedIn dbpedia:%Country1%
          }
 	 group by ?fr
         }
         {select ?fr (count (*) as ?ct2)
          where {
            ?post2 snvoc:hasCreator ?fr .
            ?post2 snvoc:creationDate ?date2 .
            filter (?date2 >= "%Date0%"^^xsd:dateTime && ?date2 < "%Date0%"^^xsd:dateTime + xsd:duration("P%Duration%D")) .
            ?post2 snvoc:isLocatedIn dbpedia:%Country2%
          }
 	 group by ?fr
         }
        {sn:pers%Person% snvoc:knows ?fr.} union {sn:pers%Person% snvoc:knows ?fr2. ?fr2 snvoc:knows ?fr. filter (?fr != sn:pers%Person%)} .
        optional { ?fr snvoc:firstName ?first } .
	optional { ?fr snvoc:lastName ?last } .
        ?fr snvoc:isLocatedIn ?city .
	filter(!exists {?city snvoc:isPartOf dbpedia:%Country1%}).
	filter(!exists {?city snvoc:isPartOf dbpedia:%Country2%}).
     }
    }.
    filter (?ct1 > 0 && ?ct2 > 0) .
}
order by desc(?sum) ?fr
limit 20
