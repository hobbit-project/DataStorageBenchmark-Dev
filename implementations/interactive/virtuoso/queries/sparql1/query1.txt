sparql select ?fr ?last (min(?dist) as ?mindist)
              ((?bday - xsd:dateTime("1970-01-01T00:00:00.000+00:00")) * 1000 as ?birthday)
              ((?since - xsd:dateTime("1970-01-01T00:00:00.000+00:00")) * 1000 as ?creationDate)
	      ?gen ?browser ?locationIP ?emails ?lngs ?based ?studyAt ?workAt #Q1
{
    ?fr a snvoc:Person . ?fr snvoc:firstName "%Name%" .
    optional { ?fr snvoc:lastName ?last } .
    optional { ?fr snvoc:birthday ?bday } .
    ?fr snvoc:creationDate ?since . ?fr snvoc:gender ?gen . ?fr snvoc:locationIP ?locationIP .
    ?fr snvoc:browserUsed ?browser .
    {
      { select distinct ?fr (1 as ?dist)
        where {
          sn:pers%Person% snvoc:knows ?fr.
        }
      }
      union
      { select distinct ?fr (2 as ?dist)
        where {
          sn:pers%Person% snvoc:knows ?fr2. ?fr2 snvoc:knows ?fr. filter (?fr != sn:pers%Person%).
        }
      }
      union
      { select distinct ?fr (3 as ?dist)
        where {
          sn:pers%Person% snvoc:knows ?fr2. ?fr2 snvoc:knows ?fr3. ?fr3 snvoc:knows ?fr. filter (?fr != sn:pers%Person%).
        }
      } .
    } .
    ?fr snvoc:isLocatedIn ?basedURI . ?basedURI foaf:name ?based .
    {
      select ?fr (group_concat (?email; separator = ", ") as ?emails)
      where {
            ?fr snvoc:email ?email .
      }
      group by ?fr
    } .
    {
      select ?fr (group_concat (?lng; separator = ", ") as ?lngs)
      where {
            ?fr snvoc:speaks ?lng .
      }
      group by ?fr
    } .
    optional {
      select ?fr (group_concat ( concat (?o_name, " ", ?year, " ", ?o_country); separator = ", ") as ?studyAt)
      where {
            ?fr snvoc:studyAt ?w .
            ?w snvoc:classYear ?year .
            ?w snvoc:hasOrganisation ?org .
            ?org snvoc:isLocatedIn ?o_countryURI .
            ?o_countryURI foaf:name ?o_country .
            ?org foaf:name ?o_name .
      }
      group by ?fr
    } .
    optional {
      select ?fr (group_concat ( concat (?o_name, " ", ?year, " ", ?o_country); separator = ", ") as ?workAt)
      where {
            ?fr snvoc:workAt ?w .
            ?w snvoc:workFrom ?year .
            ?w snvoc:hasOrganisation ?org .
            ?org snvoc:isLocatedIn ?o_countryURI .
            ?o_countryURI foaf:name ?o_country .
            ?org foaf:name ?o_name .
      }
      group by ?fr
    } .
}
group by ?fr ?last ?bday ?since ?gen ?browser ?locationIP ?emails ?lngs ?based ?studyAt ?workAt
order by ?mindist ?last ?fr
limit 20
